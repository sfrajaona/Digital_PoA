// List of properties for modelSimple.prism
// Authors: F. Rajaona, H. Treharne, I. Boureanu, S. Radomirovic
// Last modified: Sep 01, 2025
// Overview: Model checking strategic properties of PoA (Power of Attorney) 

// Property Name: Liveness to Enactment 1 (original) 
// Description: If a contract is issued, allowed, and not revoked, then (under fair scheduling) 
// the system can progress to execution:

// FALSE: Liveness to Enactment 1 (original) 
// in fact this should be TRUE if we
// allow the model to be unbounded. Eventually the verifier will execute 
// any requested action that is permitted.
filter(forall, A [ F act1_executed_A ], act1_requested_A&act1_permit_for_A_by_D&!revoked_A_by_D)

// TRUE: Liveness to Enactment 2 
// Description: Note that we need to change the inner AF into EF from Liveness to Enactment 1
filter(exists, E [ F act1_executed_A ], act1_requested_A&act1_permit_for_A_by_D&!revoked_A_by_D)

// TRUE: Liveness to Enactment 3 (authorised capture the condisions above)
filter(exists, E [ F act1_executed_A ], act1_authorised_A)

// FALSE: Liveness to Enactment 5
// Description: for any execution
filter(forall, E [ F executed_A ], act_requested_A&authorised_A)

// FALSE: Liveness to Enactment 5
// Description: for any execution + bound condition
filter(forall, E [ F executed_A ], act_requested_A&authorised_A&(req_total+1<req_max))

// Property Name: Safety under Revocation: 
// Description: After revocation, the action is never executed. 

// FALSE: Safety under Revocation 1 (original) . 
filter(forall, A [ G (!executed_A) ], revoked_A_by_D)

// TRUE: Safety under Revocation 2 (original) 
// Description: we needed to strengthen the antecedent, to make sure there is no execution happening simultaneously with the revocation
filter(forall, A [ G (!executed_A) ], revoked_A_by_D&(!executed_A))


// Property name: No "out of nothing execution"
// Description: Execution can only happen if (i) the contract was issued and (ii) not revoked
// at the decision point 

// TRUE: No "out of nothing" execution 1
A [ G executed_A=>verified_A_of_D ]

// TRUE: No "out of nothing" execution 2
A [ G act1_executed_A=>act1_permit_for_A_by_D ]

// Property name: End-to-end correctness
// Description: Issued and allowed implies that, unless revoked, there exists a strategy for the
// coalition A \cup V to achieve execution:

// TRUE: End to end correctness  1 (original + all players + bound conditions)
filter(exists, <<D,A1,V,L>>P>0 [ X act1_executed_A ], act1_requested_A&act1_permit_for_A_by_D&!revoked_A_by_D&(req_total+2<req_max)&(idling+2<idle_max))

// TRUE: End-to-end correctness 2 (modified the outer AG into EG) 
filter(exists, <<A1,V>>P>0 [ F act1_executed_A ], act1_requested_A&act1_permit_for_A_by_D&!revoked_A_by_D)

// FALSE: End to end correctness (original + bound conditions)
filter(forall, <<A1,V>>P>0 [ F act1_executed_A ], !act1_executed_A&act1_requested_A&act1_permit_for_A_by_D&!revoked_A_by_D&(req_total+2<req_max)&(idling+2<idle_max))

// FALSE: End to end correctness 
// Description: original + bound conditions + all players in strategy
filter(forall, <<D,A1,L,V>>P>0 [ F act1_executed_A ], act1_requested_A & act1_grant_cond & !act1_authorised_A & (req_total+2<req_max) & (idling+2<idle_max))

// // TRUE:  End to end correctness 
// // Description: original, but fixed the guards 
filter (forall, <<A1,V>> P>0  [F act2_executed_A], act2_authorised_A &!revoked_A_by_D & act2_requested_A) 

// FALSE:  End to end correctness
// // Description:  original, but fixed the guards, 
// Description: the property fails when V is not part of the strategic opertor
filter (forall, <<A1>> P>0  [F act2_executed_A], act2_authorised_A &!revoked_A_by_D & act2_requested_A) 

